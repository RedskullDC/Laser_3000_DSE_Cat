5  TEXT : HOME 
 10  DIM A$(200),B$(10)
 15  FOR T = 1 TO 200:A$ = "": NEXT 
 20 A = 1:B$(1) = "0":B$(2) = "00":B$(3) = "000":B$(4) = "0000"
 25 B$(5) = "00000"
 30  HTAB 08: VTAB 12: PRINT "CAT ORDERING PROGRAM V1.0"
 31  PRINT 
 35  HTAB 10: PRINT "DEVELOPED BY L.AYLING"
 36  PRINT 
 37  HTAB 15: PRINT "ON 06/09/84"
 40  FOR I = 1 TO 2000: NEXT 
 100  HOME 
 110  HTAB 10: INVERSE : PRINT "MAIN MENU"
 120  NORMAL : PRINT : PRINT "    [1] CREATE A NEW ORDER"
 130  PRINT : PRINT "    [2] ADD TO AN EXISTING ORDER"
 140  PRINT : PRINT "    [3] SCREEN OUTPUT (OR PRINTER)"
 150  PRINT : PRINT "    [4] TRANSMIT ORDER"
 155  PRINT : PRINT "    [5] SAVE AN ORDER TO DISK"
 158  PRINT : PRINT "    [6] LOAD AN ORDER FROM DISK"
 159  PRINT : PRINT "    [7] CATALOG DISK"
 160  GET AN$: IF AN$ < "1" OR AN$ > "7" THEN 100
 170  ON  VAL (AN$) GOTO 200,300,400,500,600,700,800
 200  HOME 
 201 A = 1
 205  VTAB 5: HTAB 4:M$ = "STORE ID <2 DIGITS>:"
 210  GOSUB 1000
 220  IF  LEN (T$) > 2 THEN  PRINT "TOO LONG": GOTO 205
 225 A$(A) = "10" + T$:A = A + 1:ID$ = T$
 230  HOME : VTAB 5: HTAB 4:M$ = "DATE DD/MM/YY:": GOSUB 1000
 231  IF  LEN (T$) <  > 8 THEN 230
 234 DAY$ =  LEFT$ (T$,2)
 235 DAY =  VAL (DAY$): IF DAY > 31 OR DAY < 1 THEN 230
 240 MNTH$ =  MID$ (T$,4,2)
 242 MNTH =  VAL (MNTH$): IF MNTH < 0 OR MNTH > 12 THEN 230
 245 YEAR$ =  RIGHT$ (T$,2)
 246 YEAR =  VAL (YEAR$): IF YEAR < 84 OR YEAR > 90 THEN 230
 250 A$(A) = "20" + DAY$ + MNTH$ + YEAR$ + "1" + "1" + ID$ + DAY$
 260 A = A + 1
 270  GOTO 900
 300  GOTO 900
 400  HOME 
 405  HTAB 4: VTAB 5: PRINT "SCREEN OR PRINTER OUTPUT (S/P) :";: GET YN$
 406  PRINT : PRINT : PRINT "    PRESS <CNTRL>-S TO PAUSE"
 408  IF YN$ = "P" THEN  PRINT "PR#1"
 409  PRINT : PRINT : PRINT : PRINT 
 410  PRINT "STORE ID:"; RIGHT$ (A$(1),2)
 420  PRINT "DATE :"; LEFT$ (A$(2),2);"/"; MID$ (A$(2),3,2);"/"; MID$ (A$(2),5,2)
 440  FOR T = 3 TO A - 1
 445  PRINT T - 2;
 450  PRINT " # "; MID$ (A$(T),3,5);" QTY."; RIGHT$ (A$(T),4)
 460  NEXT 
 470  PRINT "PR#0"
 475  PRINT : PRINT : PRINT 
 480  PRINT "  PRESS <RET> TO CONTINUE";
 490  INPUT YN$: GOTO 100
 500 GT$ =  STR$ (A)
 510 GT$ = B$(6 -  LEN (GT$)) + GT$
 520 A$(A) = "99" + GT$
 530  HOME : HTAB 4: VTAB 5: PRINT "CONNECT MODEM AND PRESS"
 540  HTAB 4: PRINT "<RETURN> TO TRANSMIT"
 545  HTAB 4: PRINT "<ESCAPE> FOR MENU";
 550  GET YN$
 555  IF  ASC (YN$) = 27 THEN 100
 557  PRINT 
 558  PRINT "PR#2"
 560  FOR T = 1 TO A
 565  PRINT A$(T)
 570  NEXT 
 575  PRINT "PR#0"
 580  GOTO 530
 600  HOME 
 610  VTAB 5: PRINT "PRESS <RET> WHEN DATA DISK IS READY";: GET YN$
 620 GT$ =  RIGHT$ (A$(2),5)
 625  PRINT 
 630  PRINT "OPEN ORDER ";GT$
 640  PRINT "WRITE ORDER ";GT$
 650  PRINT  STR$ (A)
 660  FOR T = 1 TO A - 1: PRINT A$(T): NEXT 
 670  PRINT "CLOSE ORDER ";GT$
 680  GOTO 100
 700  HOME 
 710  VTAB 5: PRINT "WHAT ORDER WOULD YOU LIKE RECALLED";: INPUT GT$
 720  PRINT : PRINT "PRESS <RET> WHEN DATA DISK IS READY";: GET YN$
 725  PRINT 
 730  PRINT "OPEN ORDER ";GT$
 740  PRINT "READ ORDER ";GT$
 750  INPUT A$:A =  VAL (A$)
 760  FOR T = 1 TO A - 1: INPUT A$(T): NEXT 
 770  PRINT "CLOSE ORDER ";GT$
 780  GOTO 100
 800  PRINT 
 805  PRINT "CATALOG"
 810  PRINT : PRINT : PRINT "PRESS <RET> FOR MENU";: GET YN$: GOTO 100
 900  HOME : PRINT "CAT NO FOLLOWED BY QTY <ESC> TO END"
 901 CT$ = ""
 902  PRINT "ITEM NO.";A - 2
 905  HTAB 4: VTAB 5: FOR G = 1 TO 5
 908  GET GT$
 910  IF  ASC (GT$) = 27 THEN 100
 911  IF  ASC (GT$) <  > 8 THEN CT$ = CT$ + GT$: GOTO 915
 912  IF  ASC (GT$) = 8 AND G > 2 THEN G = G - 1:CT$ =  LEFT$ (CT$,G - 1): PRINT GT$;: GOTO 908
 913 CT$ = "": GOTO 905
 915  PRINT GT$;
 920  NEXT 
 930  PRINT " QTY: ";: INPUT QTY
 940  PRINT : PRINT CT$;" : ";QTY;" CORRECT (Y/N) ";: GET YN$
 945  IF YN$ = "Y" OR  ASC (YN$) = 13 THEN 950
 946  GOTO 900
 950 QTY$ =  STR$ (QTY):QTY$ = B$(4 -  LEN (QTY$)) + QTY$
 970 A$(A) = "30" + CT$ + "0" + QTY$
 980 A = A + 1: REM  UPDATE ITEM COUNT
 990  GOTO 900
 1000  PRINT M$;: INPUT T$
 1005  PRINT 
 1010  PRINT "  ";T$;" IS THIS CORRECT (Y/N)";: GET AN$
 1020  IF AN$ <  > "Y" THEN 1000
 1030  RETURN 
